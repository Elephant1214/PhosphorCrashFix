buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.+", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"

version = "${project.mc_version}-${project.mod_version}"
group = project.mod_group
archivesBaseName = project.archive_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version

    runs {
        client {
            workingDirectory file("run")
            property "fml.coreMods.load", project.pcf_coremod
        }

        server {
            workingDirectory file("runServer")
            property "fml.coreMods.load", project.pcf_coremod
        }
    }
}

repositories {
    flatDir { dirs "libs" }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
}

jar {
    configurations.embed.each { dependency ->
        from(project.zipTree(dependency)) {
            dependency.isDirectory() ? dependency : zipTree(dependency)
        }
    }

    manifest {
        attributes([
                "FMLCorePlugin"              : project.pcf_coremod,
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod"             : true
        ])
    }
}

jar.finalizedBy("reobfJar")

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}